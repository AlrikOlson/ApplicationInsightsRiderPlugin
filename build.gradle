plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.16'
}

group 'fr.socolin'
version '1.0.2'

ext.buildType = 'stable'
// ext.buildType = 'eap'

if (ext.buildType == 'eap') {
    version = "$version-EAP"
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    if (project.ext.buildType == 'stable') {
        maven { url "https://www.jetbrains.com/intellij-repository/releases" }
    }
    if (project.ext.buildType == 'eap') {
        maven { url "https://www.jetbrains.com/intellij-repository/snapshots" }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation 'com.google.code.gson:gson:2.8.6'
}

publishPlugin {
    channels 'alpha'
    token intellijPublishToken
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    type = 'RD'

    if (project.ext.buildType == 'stable') {
        version '2019.3.4'
    } else if (project.ext.buildType == 'eap') {
        version '2020.1-EAP4-SNAPSHOT'
    }

    downloadSources = false
}
patchPluginXml {
    version = project.version
    changeNotes """
Initial version
- Display log from [Application Insights](https://docs.microsoft.com/en-us/azure/azure-monitor/app/app-insights-overview) in a tool window when debugging an application that use it.
"""
}

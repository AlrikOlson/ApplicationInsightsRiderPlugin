plugins {
    id 'java'
    id "org.jetbrains.intellij" version "0.6.5"
}

group 'fr.socolin'
version '1.0.11'

def intelijiBranch= 'eap'
if (project.hasProperty('buildType')) {
    intelijiBranch = project.property('buildType');
}

if (intelijiBranch == 'eap') {
    version = "$version-EAP"
}

sourceCompatibility = 1.11

repositories {
    mavenCentral()
    if (intelijiBranch == 'stable') {
        maven { url "https://www.jetbrains.com/intellij-repository/releases" }
    }
    if (intelijiBranch == 'eap') {
        maven { url "https://www.jetbrains.com/intellij-repository/snapshots" }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation 'com.google.code.gson:gson:2.8.6'
}

publishPlugin {
    if (intelijiBranch == 'eap') {
        channels 'eap'
    } else {
        channels 'stable'
    }
    token intellijPublishToken
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    type = 'RD'

    if (intelijiBranch == 'stable') {
        version '2020.3'
    } else if (intelijiBranch == 'eap') {
        version '2020.3-EAP10-SNAPSHOT'
    }

    downloadSources = false
}
patchPluginXml {
    version = project.version
    changeNotes """
<ul>
    <li>1.0.11</li>
    <ul>
        <li>Compatible with Rider 2020.3</li>
    </ul>
</ul>
"""
}
